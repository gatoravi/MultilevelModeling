# Bugs model for hiv data

model {
  for (i in 1:n){
    y[i] ~ dnorm (y.hat[i], tau.y)
    y.hat[i] <- xi.a*a[person[i]] + xi.b[treatment[person[i]]]*b[person[i]]*time[i]
  }
  tau.y <- pow(sigma.y,-2)
  sigma.y ~ dunif (0, 100)
  for (j in 1:J){
    a[j] ~ dnorm (mu.a, tau.a)
    a.adj[j] <- xi.a*a[j]
    b[j] ~ dnorm (mu.b[treatment[j]], tau.b[treatment[j]])
    b.adj[j] <- xi.b[treatment[j]]*b[j]
  }
  mu.a ~ dnorm (0, .0001)
  mu.a.adj <- xi.a*mu.a
  xi.a ~ dnorm (0, .0001)
  tau.a <- pow(sigma.a,-2)
  sigma.a ~ dunif (0, 100)
  sigma.a.adj <- abs(xi.a)*sigma.a
  for (k in 1:2){
    mu.b[k] ~ dnorm (0, .0001)
    mu.b.adj[k] <- xi.b[k]*mu.b[k]
    xi.b[k] ~ dnorm (0, .0001)
    tau.b[k] <- pow(sigma.b[k],-2)
    sigma.b[k] ~ dunif (0, 100)
    sigma.b.adj[k] <- abs(xi.b[k])*sigma.b[k]
  }
  diff <- mu.b.adj[1] - mu.b.adj[2]
}
 